plugins {
    id 'fabric-loom' version '0.7-SNAPSHOT'
    id 'idea'
    id "me.hypherionmc.modutils.modpublisher" version "1.0.7"
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

archivesBaseName = "${mod_name}-fabric-${minecraft_version}"

configurations {
    shade
    implementation.extendsFrom shade
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    modImplementation "maven.modrinth:fabrictailor:1.5.0"

    shade("me.hypherionmc.sdlink:sdlink-lib:${library_ver}h")
    shade "me.hypherionmc.sdlink:mcdiscordformatter-fabric-1.16.5:${formatter_ver}"
}

shadowJar {
    from sourceSets.main.output

    configurations = [project.configurations.shade]
}

remapJar {
    dependsOn(shadowJar)
    input.set shadowJar.archiveFile.get()
}

loom {
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}


processResources {
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }

    filesMatching('*.mixins.json') {
        expand "refmap_target": "${archivesBaseName}-"
    }
}

jar {
    from("LICENSE") {
        rename { "${it}_${mod_name}" }
    }
}

task delDevJar {
    doLast {
        def tree = fileTree('build/libs')
        tree.include '**/*-dev.jar'
        tree.include '**/*-all.jar'
        tree.each { it.delete() }
    }
}
build.finalizedBy delDevJar

publisher {
    apiKeys {
        modrinth = System.getenv("MODRINTH_TOKEN")
        curseforge = System.getenv("CURSE_TOKEN")
    }

    curseID = 541320
    modrinthID = modrinth_id
    versionType = "release"
    changelog = rootProject.file("changelog-fabric.md")
    version = "${minecraft_version}-${version}"
    displayName = "[FABRIC/QUILT 1.16.5] Simple Discord Link - ${project.version}"
    gameVersions = ["1.16.5"]
    loaders = ["fabric", "quilt"]
    artifact = remapJar

    modrinthDepends {
        required = ["P7dR8mSH"]
    }

    curseDepends {
        required = ["fabric-api"]
    }
}

publishMod.dependsOn(build)
