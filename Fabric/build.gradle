plugins {
    id 'fabric-loom' version '1.1-SNAPSHOT'
    id 'idea'
    id "com.modrinth.minotaur" version "2.+"
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id "me.hypherionmc.cursegradle" version "2.0.1"
}

archivesBaseName = "${mod_name}-fabric-${minecraft_version}"

configurations {
    shade
    implementation.extendsFrom shade
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    modCompileOnly "maven.modrinth:fabrictailor:2.0.1"

    implementation project(":Common")

    shade("me.hypherionmc.sdlink:sdlink-lib:${library_ver}")
    shade "me.hypherionmc.sdlink:mcdiscordformatter-fabric-1.19.1:${formatter_ver}"
}

shadowJar {
    from sourceSets.main.output

    configurations = [project.configurations.shade]
}

remapJar {
    dependsOn(shadowJar)
    shouldRunAfter(shadowJar)
    input.set shadowJar.archiveFile.get()
}

loom {
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}


processResources {
    from project(":Common").sourceSets.main.resources
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }

    filesMatching('*.mixins.json') {
        expand "refmap_target": "${archivesBaseName}-"
    }
}

tasks.withType(JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
}

jar {
    from("LICENSE") {
        rename { "${it}_${mod_name}" }
    }
}

task delDevJar {
    doLast {
        def tree = fileTree('build/libs')
        tree.include '**/*-dev.jar'
        tree.include '**/*-all.jar'
        tree.each { it.delete() }
    }
}
build.finalizedBy delDevJar

modrinth {
    debugMode = false
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "Sh0YauEf"
    versionNumber = "${minecraft_version}-${version}f"
    versionType = "release"
    changelog = rootProject.file("changelog-fabric.md").text.trim()
    uploadFile = remapJar
    versionName = "[Fabric/Quilt 1.19.3] Simple Discord Link - ${project.version}"
    gameVersions = ["1.19.3"]
    loaders = [ "fabric", "quilt" ]
    dependencies {
        required.project "P7dR8mSH"
    }
}

curseforge {
    if (System.getenv("CURSE_TOKEN") !== null) {
        apiKey = System.getenv("CURSE_TOKEN")
    } else {
        apiKey = ""
    }
    project {
        id = "541320"
        releaseType = "release"
        addGameVersion "1.19.3"
        addGameVersion "Fabric"
        addGameVersion "Quilt"
        addGameVersion "Java 17"
        changelogType = "markdown"
        changelog = rootProject.file("changelog-fabric.md")
        mainArtifact(remapJar) {
            displayName = "[Fabric/Quilt 1.19.3] Simple Discord Link - ${project.version}"
        }
    }
    options {
        forgeGradleIntegration = false
        debug = false
        fabricIntegration = false
    }
}

afterEvaluate {
    tasks.curseforge541320.dependsOn remapJar
}
