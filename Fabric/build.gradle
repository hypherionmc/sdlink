plugins {
    id 'fabric-loom' version '1.3-SNAPSHOT'
    id 'idea'
    id "me.hypherionmc.modutils.modpublisher" version "1.0.+"
}

apply plugin: 'com.github.johnrengelman.shadow'

archivesBaseName = "${mod_name}-fabric-${minecraft_version}"

configurations {
    shade
    implementation.extendsFrom(shade)
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    implementation project(":Common")

    modCompileOnly "maven.modrinth:fabrictailor:2.1.2"
    modImplementation "com.hypherionmc.craterlib:CraterLib-fabric-1.20.2:${craterlib}"

    shade("me.hypherionmc.sdlink:sdlink-lib:${library_ver}")
    shade "me.hypherionmc.sdlink:mcdiscordformatter-fabric-1.19.1:${formatter_ver}"
}

loom {
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

processResources {
    from project(":Common").sourceSets.main.resources
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }

    filesMatching('*.mixins.json') {
        expand "refmap_target": "${archivesBaseName}-"
    }
}

tasks.withType(JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
}

jar {
    from("LICENSE") {
        rename { "${it}_${mod_name}" }
    }
}

/**
 * Shadowjar Config
 */
shadowJar {
    configurations = [project.configurations.shade]
    setArchiveClassifier("dev-shadow")
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    setArchiveClassifier(null)
}

/**
 * Publishing Config
 */
publisher {
    apiKeys {
        modrinth = System.getenv("MODRINTH_TOKEN")
        curseforge = System.getenv("CURSE_TOKEN")
    }

    curseID = 541320
    modrinthID = modrinth_id
    versionType = "release"
    changelog = rootProject.file("changelog-fabric.md")
    version = "${minecraft_version}-${project.version}"
    displayName = "[FABRIC/QUILT 1.20.2] Simple Discord Link - ${project.version}"
    gameVersions = ["1.20.2"]
    loaders = ["fabric", "quilt"]
    artifact = remapJar

    modrinthDepends {
        required = ["P7dR8mSH", "Nn8Wasaq"]
    }

    curseDepends {
        required = ["fabric-api", "craterlib"]
    }
}

publishModrinth.dependsOn(remapJar)
publishCurseforge.dependsOn(remapJar)