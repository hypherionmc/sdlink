plugins {
    id "com.hypherionmc.modutils.modfusioner" version "1.0.+"
    id "me.hypherionmc.modutils.modpublisher" version "1.0.+"
}

ext {
    release=project.properties['release'] ?: false
}

var base_version = "${version_major}.${version_minor}"
var final_version = "${base_version}.${version_patch}"

// CI Support
if (!release && System.getenv('BUILD_NUMBER') != null) {
    final_version = "${base_version}.${System.getenv('BUILD_NUMBER')}d"
}

fusioner {
    packageGroup = project.group
    mergedJarName = "SimpleDiscordLink-combo-${minecraft_version}"
    //outputDirectory = "artifacts/merged"
    jarVersion = final_version

    forge {
        projectName = "Forge"
        inputTaskName = "jar"

        mixin "${mod_id}.mixins.json"
        mixin "${mod_id}-forge.mixins.json"

        addRelocate "me.hypherionmc.mcdiscordformatter", "forge.me.hypherionmc.mcdiscordformatter"
    }

    fabric {
        projectName = "Fabric"
        inputTaskName = "remapJar"

        addRelocate "me.hypherionmc.mcdiscordformatter", "fabric.me.hypherionmc.mcdiscordformatter"
    }

    relocateDuplicate "com.hypherionmc.sdlink.shaded"
}

subprojects {
    apply plugin: 'java'

    java.toolchain.languageVersion = JavaLanguageVersion.of(17)

    version = "${final_version}"

    repositories {
        mavenCentral()
        maven {
            name = 'First Dark Maven'
            url = 'https://maven.firstdark.dev/releases/'
        }
        maven {
            name = 'First Dark Maven Snapshots'
            url = 'https://maven.firstdark.dev/snapshots/'
        }

        maven {
            name = 'First Dark Mirror'
            url = 'https://mcentral.firstdark.dev/releases/'
        }

        maven {
            name = "Modrinth"
            url = "https://api.modrinth.com/maven"
            content {
                includeGroup "maven.modrinth"
            }
        }
    }

    jar {
        manifest {
            attributes([
                    'Specification-Title'     : mod_id,
                    'Specification-Vendor'    : mod_author,
                    'Specification-Version'   : project.jar.archiveVersion,
                    'Implementation-Title'    : project.name,
                    'Implementation-Version'  : project.jar.archiveVersion,
                    'Implementation-Vendor'   : mod_author,
                    'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                    'Timestamp'               : System.currentTimeMillis(),
                    'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                    'Build-On-Minecraft'      : minecraft_version
            ])
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.release = 17
    }

    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    clean {
        delete "$rootDir/artifacts"
    }

    task copyAllArtifacts(type: Copy) {
        if (project.name !== "Common") {
            from "$buildDir/libs"
            into "$rootDir/artifacts"
            include("*.jar")
            delete("$rootDir/artifacts/*-common-*.jar")
        }
    }

    build.finalizedBy(copyAllArtifacts)
}

publisher {
    apiKeys {
        modrinth = System.getenv("MODRINTH_TOKEN")
        curseforge = System.getenv("CURSE_TOKEN")
    }

    curseID = 541320
    modrinthID = modrinth_id
    versionType = "release"
    changelog = rootProject.file("changelog.md")
    version = "${minecraft_version}-${project.version}"
    displayName = "[1.19.x] Simple Discord Link - ${project.version}"
    gameVersions = ["1.19.2", "1.19.3", "1.19.4"]
    loaders = ["forge", "fabric", "quilt"]
    artifact = fusejars

    modrinthDepends {
        required = ["Nn8Wasaq"]
    }

    curseDepends {
        required = ["craterlib"]
    }
}

publishCurseforge.dependsOn(fusejars)
publishModrinth.dependsOn(fusejars)